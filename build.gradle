plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.0-SNAPSHOT" apply false
    
    id "com.modrinth.minotaur" version "2.+"
    id "io.github.CDAGaming.cursegradle" version "1.6.0"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "com.matthewprenger.cursegradle"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-$rootProject.minecraft_version:$rootProject.parchment_version@zip")
        }
    }
    
    var env = System.getenv()
    var modLoader = name
    
    var changelogFile = rootProject.file("changelog.md")
    var changelogText = changelogFile.exists() ? changelogFile.text : ""
    
    if (env.MODRINTH_TOKEN) {
        modrinth {
            projectId = env.MODRINTH_PROJECT
            uploadFile = tasks.remapJar
            gameVersions = [minecraft_version]
            loaders = [modLoader]
            changelog = changelogText
            debugMode = true
            
            if (modLoader == "fabric") {
                dependencies {
                    required.project "resourceful-config"
                }
            } else {
                dependencies {
                    required.project "resourceful-config"
                }
            }
        }
    }
    
    if (env.CURSE_TOKEN) {
        curseforge {
            apiKey = env.CURSE_TOKEN
            project {
                id = env.CURSE_PROJECT
                changelog = changelogText
                releaseType = "release"
                addGameVersion minecraft_version
                addGameVersion modLoader.capitalize()
                debug = true
                
                mainArtifact(tasks.remapJar)
                
                if (modLoader == "fabric") {
                    relations {
                        requiredDependency "resourceful-config"
                    }
                } else {
                    relations {
                        requiredDependency "resourceful-config"
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://maven.terraformersmc.com" }
        maven { url "https://nexus.resourcefulbees.com/repository/maven-public/" }
        maven { url "https://maven.isxander.dev/releases" }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}